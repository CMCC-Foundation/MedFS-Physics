!/ ------------------------------------------------------------------- /
      MODULE W3SRCEMD
!/
!/                  +-----------------------------------+
!/                  | WAVEWATCH III           NOAA/NCEP |
!/                  |           H. L. Tolman            |
!/                  |            F. Ardhuin             |
!/                  |                        FORTRAN 90 |
!/                  | Last update :         29-May-2009 |
!/                  +-----------------------------------+
!/
!/    For updates see subroutine.
!/
!  1. Purpose :
!
!     Source term integration routine.
!
!  2. Variables and types :
!
!      Name      Type  Scope    Description
!     ----------------------------------------------------------------
!      OFFSET    R.P.  Private  Offset in time integration scheme.
!                               0.5 in original WAM, now 1.0
!     ----------------------------------------------------------------
!
!  3. Subroutines and functions :
!
!      Name      Type  Scope    Description
!     ----------------------------------------------------------------
!      W3SRCE    Subr. Public   Calculate and integrate source terms.
!     ----------------------------------------------------------------
!
!  4. Subroutines and functions used :
!
!     See corresponding documentation of W3SRCE.
!
!  5. Remarks :
!
!  6. Switches :
!
!       See section 9 of W3SRCE.
!
!  7. Source code :
!
!/ ------------------------------------------------------------------- /
!/
      REAL, PARAMETER, PRIVATE:: OFFSET = 1.
!/
      CONTAINS
!/ ------------------------------------------------------------------- /
      SUBROUTINE W3SRCE ( IX, IY, IMOD, SPEC, ALPHA, WN1, CG1, DEPTH, &
                          U10ABS, U10DIR, AS, USTAR, USTDIR, CX, CY,  &
                          EMEAN, FMEAN, WNMEAN, AMAX, FPI, CD, Z0,    &
                          DTDYN, FCUT, DTG )
!/
!/                  +-----------------------------------+
!/                  | WAVEWATCH III           NOAA/NCEP |
!/                  |           H. L. Tolman            |
!/                  |            F. Ardhuin             |
!/                  |                        FORTRAN 90 |
!/                  | Last update :         29-May-2009 |
!/                  +-----------------------------------+
!/
!/    06-Dec-1996 : Final FORTRAN 77                    ( version 1.18 )
!/    04-Feb-2000 : Upgrade to FORTRAN 90               ( version 2.00 )
!/    14-Feb-2000 : Exact-NL added                      ( version 2.01 )
!/    04-May-2000 : Non-central integration             ( version 2.03 )
!/    02-Feb-2001 : Xnl version 3.0                     ( version 2.07 )
!/    09-May-2002 : Switch clean up.                    ( version 2.21 )
!/    13-Nov-2002 : Add stress vector.                  ( version 3.00 )
!/    27-Nov-2002 : First version of VDIA and MDIA.     ( version 3.01 )
!/    07-Oct-2003 : Output options for NN training.     ( version 3.05 )
!/    24-Dec-2004 : Multiple model version.             ( version 3.06 )
!/    23-Jun-2006 : Linear input added.                 ( version 3.09 )
!/    27-Jun-2006 : Adding file name preamble.          ( version 3.09 )
!/    04-Jul-2006 : Separation of stress computation.   ( version 3.09 )
!/    16-Apr-2007 : Miche style limiter added.          ( version 3.11 )
!/                  (J. H. Alves)
!/    25-Apr-2007 : Battjes-Janssen Sdb added.          ( version 3.11 )
!/                  (J. H. Alves)
!/    09-Oct-2007 : Adding WAM 4+ and SB1 options.      ( version 3.13 )
!/                  (F. Ardhuin)
!/    29-May-2009 : Preparing distribution version.     ( version 3.14 )
!/
!/    Copyright 2009 National Weather Service (NWS),
!/       National Oceanic and Atmospheric Administration.  All rights
!/       reserved.  WAVEWATCH III is a trademark of the NWS. 
!/       No unauthorized use without permission.
!/
!  1. Purpose :
!
!     Calculate and integrate source terms for a single grid point.
!
!  2. Method :
!
!     Physics  : see manual and corresponding subroutines.
!
!     Numerics :
!
!     Dynamic-implicit integration of the source terms based on
!     WW-II (Tolman 1992). The dynamic time step is calculated
!     given a maximum allowed change of spectral densities for
!     frequencies / wavenumbers below the usual cut-off.
!     The maximum change is given by the minimum of a parametric
!     and a relative change. The parametric change relates to a
!     PM type equilibrium range
!
!                                -1  (2pi)**4       1
!       dN(k)     =  Xp alpha  pi   ---------- ------------
!            max                       g**2     k**3 sigma
!
!                              1                                     .
!                 =  FACP ------------                              (1)
!                          k**3 sigma                                .
!
!     where
!           alpha = 0.62e-4                       (set in W3GRID)
!           Xp      fraction of PM shape          (read in W3GRID)
!           FACP    combined factor               (set in W3GRID)
!
!     The maximum relative change is given as
!
!                           /            +-                  -+ \    .
!       dN(k)     =  Xr max | N(k) , max | Nx , Xfilt N(k)    | |   (2)
!            max            \            +-               max-+ /    .
!
!     where
!           Xr      fraction of relative change   (read in W3GRID)
!           Xfilt   filter level                  (read in W3GRID)
!           Nx      Maximum parametric change (1)
!                   for largest wavenumber.
!
!  3. Parameters :
!
!     Parameter list
!     ----------------------------------------------------------------
!       IX,IY   Int.   I   Discrete grid point counters.
!       IMOD    Int.   I   Model number.
!       SPEC    R.A.  I/O  Spectrum (action) in 1-D form.
!       ALPHA   R.A.  I/O  Nondimenional 1-D spectrum corresponding
!                          to above full spectra (Phillip's const.).
!                          Calculated separately for numerical
!                          economy on vector machine (W3SPR2).
!       WN1     R.A.   I   Discrete wavenumbers.
!       CG1     R.A.   I   Id. group velocities.
!       DEPTH   Real.  I   Depth.
!       U10ABS  Real.  I   Wind speed at reference height.
!       U10DIR  Real.  I   Id. wind direction.
!       AS      Real.  I   Air-sea temp. difference.      ( !/ST3 )
!       USTAR   Real. !/O  Friction velocity.
!       USTDIR  Real  !/O  Idem, direction.
!       CX-Y    Real.  I   Current velocitu components.   ( !/BS1 )
!       EMEAN   Real. I/O  Mean energy.                   ( !/ST1 )
!       FMEAN   Real. I/O  Mean frequency.                ( !/ST1 )
!       WNMEAN  Real. I/O  Mean wavenumber.       ( !/ST1 , !/ST2 )
!       AMAX    Real.  O   Maximum energy.        ( !/ST1 , !/ST2 )
!       FPI     Real  I/O  Peak-input frequency.          ( !/ST2 )
!       CD      Real  I/O  Drag coefficient.              ( !/ST2 )
!       Z0      Real  I/O  Roughness length.              ( !/ST2 )
!       DTDYN   Real   O   Average dynamic time step.
!       FCUT    Real   O   Cut-off frequency for tail.
!       DTG     Real   I   Global time step.
!     ----------------------------------------------------------------
!       Note: several pars are set to I/O to avoid compiler warnings.
!
!  4. Subroutines used :
!
!      Name      Type  Module   Description
!     ----------------------------------------------------------------
!      W3SPRn    Subr. W3SRCnMD Mean wave parameters for use in
!                               source terms.
!      W3FLXn    Subr. W3FLXnMD Flux/stress computation.
!      W3SLNn    Subr. W3SLNnMD Linear input.
!      W3SINn    Subr. W3SRCnMD Input source term.
!      W3SNLn    Subr. W3SNLnMD Nonlinear interactions.
!      W3SDSn    Subr. W3SRCnMD Whitecapping source term
!      W3SBTn    Subr. W3SBTnMD Bottom friction source term.
!      W3SDBn    Subr. W3SBTnMD Depth induced breaking source term.
!      W3STRn    Subr. W3STRnMD Triad interaction source term.
!      W3SBSn    Subr. W3SBSnMD Bottom scattering source term.
!      W3SXXn    Subr. W3SXXnMD Unclassified source term.
!      STRACE    Subr. W3SERVMD Subroutine tracing (!/S)
!     ----------------------------------------------------------------
!
!  5. Called by :
!
!      Name      Type  Module   Description
!     ----------------------------------------------------------------
!      W3WAVE    Subr. W3WAVEMD Actual wave model routine.
!     ----------------------------------------------------------------
!
!  6. Error messages :
!
!       None.
!
!  7. Remarks :
!
!     - No testing is performed on the status of the grid point.
!
!  8. Structure :
!
!     -----------------------------------------------------------------
!       1.   Preparations
!         a  Set maximum change and wavenumber arrays.
!         b  Prepare dynamic time stepping.
!         c  Compute mean parameters.                       ( W3SPRn )
!         d  Compute stresses (if posible).
!         e  Prepare cut-off
!         f  Test output for !/NNT option.
!     --start-dynamic-integration-loop---------------------------------
!       2.  Calculate source terms
!         a Input.                                  ( W3SLNx, W3SINn )
!         b Nonlinear interactions.                         ( W3SNLn )
!         c Dissipation.                                    ( W3SDSn )
!         d Bottom friction.                                ( W3SBTn )
!       3.  Calculate cut-off frequencie(s)
!       4.  Summation of source terms and diagonal term and time step.
!       5.  Increment spectrum.
!       6.  Add tail
!         a Mean wave parameters and cut-off                ( W3SPRn )
!         b 'Seeding' of spectrum.                          ( !/SEED )
!         c Add tail
!       7.  Check if integration complete.
!     --end-dynamic-integration-loop-----------------------------------
!       8.  Save integration data.
!     -----------------------------------------------------------------
!
!  9. Switches :
!
!     !/FLX1  Wu (1980) stress computation.              ( Choose one )
!     !/FLX2  T&C (1996) stress computation.
!     !/FLX3  T&C (1996) stress computation with cap.
!
!     !/LN0   No linear input.                           ( Choose one )
!     !/LNX   User-defined bottom friction.
!
!     !/ST0   No input and dissipation.                  ( Choose one )
!     !/ST1   WAM-3 input and sissipation.
!     !/ST2   Tolman and Chalikov (1996)  input and sissipation.
!     !/ST3   WAM 4+ input and dissipation.
!     !/STX   User-defined input and sissipation.
!
!     !/NL0   No nonlinear interactions.                 ( Choose one )
!     !/NL1   Discrete interaction approximation.
!     !/NL2   Exact nonlinear interactions.
!     !/NLX   User-defined nonlinear interactions.
!
!     !/BT0   No bottom friction.                        ( Choose one )
!     !/BT1   JONSWAP bottom friction.
!     !/BTX   User-defined bottom friction.
!
!     !/DB0   No depth-limited breaking.                 ( Choose one )
!     !/DB1   Battjes-Janssen depth-limited breaking.
!     !/DBX   User-defined bottom friction.
!
!     !/TR0   No triad interactions.                     ( Choose one )
!     !/TRX   User-defined bottom friction.
!
!     !/BS0   No bottom scattering.                      ( Choose one )
!     !/BS1   Scattering term by F. Ardhuin.
!     !/BSX   User-defined bottom friction.
!
!     !/XX0   No arbitrary additional source term.       ( Choose one )
!     !/XXX   User-defined bottom friction.
!
!     !/MLIM  Miche style limiter for shallow water and steepness.
!
!     !/SEED  'Seeding' of lowest frequency for suffuciently strong
!             winds.
!
!     !/NNT   Write output to file test_data_NNN.ww3 for NN training.
!
!     !/S     Enable subroutine tracing.
!     !/T     Enable general test output.
!
! 10. Source code :
!
!/ ------------------------------------------------------------------- /
      USE CONSTANTS
      USE W3GDATMD, ONLY: NK, NTH, NSPEC, SIG, TH, DTMAX, DTMIN,      &
                          FACTI1, FACTI2, FACSD, FACHFA, FACP,        &
                          XFC, XFLT, XREL, XFT, FXFM, FXPM, DDEN,     &
                          FTE, FTF, FHMAX
      USE W3WDATMD, ONLY: TIME
      USE W3ODATMD, ONLY: NDSE, NDST
!/NNT      USE W3ODATMD, ONLY: IAPROC, SCREEN, FNMPRE
!/FLX1      USE W3FLX1MD
!/FLX2      USE W3FLX2MD
!/FLX3      USE W3FLX3MD
!/LN1      USE W3SLN1MD
!/LNX      USE W3SLNXMD
!/ST0      USE W3SRC0MD
!/ST1      USE W3SRC1MD
!/ST2      USE W3SRC2MD
!/ST2      USE W3GDATMD, ONLY : ZWIND
!/ST3      USE W3SRC3MD
!/ST3      USE W3GDATMD, ONLY : ZZWND
!/STX      USE W3SRCXMD
!/NL1      USE W3SNL1MD
!/NL2      USE W3SNL2MD
!/NLX      USE W3SNLXMD
!/BT1      USE W3SBT1MD
!/BTX      USE W3SBTXMD
!/DB1      USE W3SDB1MD
!/DBX      USE W3SDBXMD
!/TRX      USE W3STRXMD
!/BS1      USE W3SBS1MD
!/BSX      USE W3SBSXMD
!/XXX      USE W3SXXXMD
!/S      USE W3SERVMD, ONLY: STRACE
!/NNT      USE W3SERVMD, ONLY: EXTCDE
!/
      IMPLICIT NONE
!/
!/ ------------------------------------------------------------------- /
!/ Parameter list
!/
      INTEGER, INTENT(IN)     :: IX, IY, IMOD
      REAL, INTENT(IN)        :: WN1(NK), CG1(NK), DEPTH, U10ABS,     &
                                 U10DIR, AS, CX, CY, DTG
      REAL, INTENT(INOUT)     :: SPEC(NSPEC), ALPHA(NK), EMEAN,       &
                                 FMEAN, WNMEAN, USTAR, USTDIR, FPI,   &
                                 CD, Z0
      REAL, INTENT(OUT)       :: AMAX, DTDYN, FCUT
!/
!/ ------------------------------------------------------------------- /
!/ Local parameters
!/
      INTEGER                 :: IK, ITH, IS, IS0, NSTEPS,  NKH, NKH1,&
                                 NSPECH, IDT, IERR
!/S      INTEGER, SAVE           :: IENT = 0
!/NNT      INTEGER, SAVE           :: NDSD = 89, NDSD2 = 88, J
      REAL                    :: DTTOT, FHIGH, DT, AFILT, DAMAX, AFAC,&
                                 HDT, ZWND, FP
!/ST1      REAL                    :: FH1, FH2, EBAND
!/ST2      REAL                    :: FHTRAN, DFH, FACDIA, FACPAR
!/ST3      REAL                    :: FMEANS, FMEANWS, TAUWX, TAUWY, FH1, FH2
      REAL                    :: QCERR  = 0.     !/XNL2 and !/NNT
!/SEED      REAL                    :: UC, SLEV
!/MLIM      REAL                    :: HM, EM
!/NNT      REAL                    :: FACNN
!/T      REAL                    :: DTRAW
      REAL                    :: DAM (NSPEC), WN2 (NSPEC),            &
                                 VSLN(NSPEC),                         &
                                 VSIN(NSPEC), VDIN(NSPEC),            &
                                 VSNL(NSPEC), VDNL(NSPEC),            &
                                 VSDS(NSPEC), VDDS(NSPEC),            &
                                 VSBT(NSPEC), VDBT(NSPEC),            &
!/DB1                                 VSDB(NSPEC), VDDB(NSPEC),            &
!/DBX                                 VSDB(NSPEC), VDDB(NSPEC),            &
!/TRX                                 VSTR(NSPEC), VDTR(NSPEC),            &
!/BS1                                 VSBS(NSPEC), VDBS(NSPEC),            &
!/BSX                                 VSBS(NSPEC), VDBS(NSPEC),            &
!/XXX                                 VSXX(NSPEC), VDXX(NSPEC),            &
                                 VS  (NSPEC), VD  (NSPEC), EB(NK)
!/ST3      LOGICAL                :: LLWS(NSPEC) 
      REAL                    :: FOUT(NK,NTH), SOUT(NK,NTH), DOUT(NK,NTH)
      LOGICAL, SAVE           :: FLTEST = .FALSE., FLAGNN = .TRUE.
      LOGICAL                 :: SHAVE
!/ST3      LOGICAL, SAVE           :: FIRST = .TRUE.
!/NNT      LOGICAL, SAVE           :: FIRST = .TRUE.
!/NNT      CHARACTER(LEN=17), SAVE :: FNAME = 'test_data_nnn.ww3'
!/
!/ ------------------------------------------------------------------- /
!/
!/S      CALL STRACE (IENT, 'W3SRCE')
!
!/T      FLTEST = .TRUE.
!
!/LN0      VSLN = 0.
!/SEED      VSLN = 0.
!/ST0      VSIN = 0.
!/ST0      VDIN = 0.
!/NL0      VSNL = 0.
!/NL0      VDNL = 0.
!/ST0      VSDS = 0.
!/ST0      VDDS = 0.
!/BT0      VSBT = 0.
!/BT0      VDBT = 0.
!
!/ST0      ZWND   = 10.
!/ST1      ZWND   = 10.
!/ST2      ZWND   = ZWIND
!
!/T      WRITE (NDST,9000)
!/T      WRITE (NDST,9001) DEPTH, U10ABS, U10DIR*RADE
!
! 1.  Preparations --------------------------------------------------- *
!
! 1.a Set maximum change and wavenumber arrays.
!
      DO IK=1, NK
        DAM(1+(IK-1)*NTH) = FACP / ( SIG(IK) * WN1(IK)**3 )
        WN2(1+(IK-1)*NTH) = WN1(IK)
        END DO
!
      DO IK=1, NK
        IS0    = (IK-1)*NTH
        DO ITH=2, NTH
          DAM(ITH+IS0) = DAM(1+IS0)
          WN2(ITH+IS0) = WN2(1+IS0)
          END DO
        END DO
!
! 1.b Prepare dynamic time stepping
!
      DTDYN  = 0.
      DTTOT  = 0.
      NSTEPS = 0
!
! 1.c Set mean parameters
!
!/ST0      CALL W3SPR0 (SPEC, CG1, WN1, EMEAN, FMEAN, WNMEAN, AMAX)
!/ST0      FP     = 0.85 * FMEAN
!/ST1      CALL W3SPR1 (SPEC, CG1, WN1, EMEAN, FMEAN, WNMEAN, AMAX)
!/ST1      FP     = 0.85 * FMEAN
!/ST2      CALL W3SPR2 (SPEC, CG1, WN1, DEPTH, FPI, U10ABS, USTAR,    &
!/ST2                   EMEAN, FMEAN, WNMEAN, AMAX, ALPHA, FP )
!/ST3      TAUWX=0.
!/ST3      TAUWY=0.
!/ST3      IF ( FIRST ) THEN
!/ST3          FIRST  = .FALSE.
!/ST3          LLWS(:) = .TRUE.
!/ST3      ELSE
!/ST3        CALL W3SPR3 (SPEC, CG1, WN1, EMEAN, FMEAN, FMEANS, WNMEAN, &
!/ST3                   AMAX, U10ABS, U10DIR, USTAR, USTDIR,          &
!/ST3                   TAUWX, TAUWY, CD, Z0, LLWS, FMEANWS)
!/ST3        CALL W3SIN3 ( SPEC, CG1, WN2, U10ABS, USTAR, DAIR/DWAT, AS,&
!/ST3                 U10DIR, Z0, CD, TAUWX, TAUWY, VSIN, VDIN, LLWS )
!/ST3        END IF
!/ST3      CALL W3SPR3 (SPEC, CG1, WN1, EMEAN, FMEAN, FMEANS, WNMEAN, &
!/ST3                   AMAX, U10ABS, U10DIR, USTAR, USTDIR,          &
!/ST3                   TAUWX, TAUWY, CD, Z0, LLWS, FMEANWS)
!
! 1.d Stresses
!
!/FLX1      CALL W3FLX1 ( ZWND, U10ABS, U10DIR, USTAR, USTDIR, Z0, CD )
!/FLX2      CALL W3FLX2 ( ZWND, DEPTH, FP, U10ABS, U10DIR,            &
!/FLX2                                          USTAR, USTDIR, Z0, CD )
!/FLX3      CALL W3FLX3 ( ZWND, DEPTH, FP, U10ABS, U10DIR,            &
!/FLX3                                          USTAR, USTDIR, Z0, CD )
!
! 1.e Prepare cut-off 
!
!/ST0      FHIGH  = SIG(NK)
!/ST1      FH1    = FXFM * FMEAN
!/ST1      FH2    = FXPM / USTAR
!/ST1      FHIGH  = MAX ( FH1 , FH2 )
!/ST1      IF (FLTEST) WRITE (NDST,9004) FH1*TPIINV, FH2*TPIINV, FHIGH*TPIINV
!/ST2      FHIGH  = XFC * FPI
!/ST3      FHIGH  = FXFM * MAX(FMEAN,FMEANWS)
!
! 1.f Prepare output file for !/NNT option
!
!/NNT      IF ( FIRST ) THEN
!/NNT          J      = LEN_TRIM(FNMPRE)
!/NNT          WRITE (FNAME(11:13),'(I3.3)') IAPROC
!/NNT          OPEN (NDSD,FILE=FNMPRE(:J)//FNAME,FORM='UNFORMATTED',   &
!/NNT                ERR=800,IOSTAT=IERR)
!/NNT          WRITE (NDSD,ERR=801,IOSTAT=IERR) NK, NTH
!/NNT          WRITE (NDSD,ERR=801,IOSTAT=IERR) SIG(1:NK) * TPIINV
!/NNT          FIRST  = .FALSE.
!/NNT          OPEN (NDSD2,FILE=FNMPRE(:J)//'time.ww3',                &
!/NNT                FORM='FORMATTED',ERR=800,IOSTAT=IERR)
!/NNT        END IF
!
! ... Branch point dynamic integration - - - - - - - - - - - - - - - -
!
      DO
!
        NSTEPS = NSTEPS + 1
!
!/T        WRITE (NDST,9020) NSTEPS, DTTOT
!
! 2.  Calculate source terms ----------------------------------------- *
!
! 2.a Input.
!
!/LN1        CALL W3SLN1 (       WN1, FHIGH, USTAR, U10DIR , VSLN       )
!/LNX        CALL W3SLNX
!
!/ST1        CALL W3SIN1 ( SPEC, WN2, USTAR, U10DIR ,        VSIN, VDIN )
!/ST2        CALL W3SIN2 ( SPEC, CG1, WN2, U10ABS, U10DIR, CD, Z0,        &
!/ST2                                                   FPI, VSIN, VDIN )
!/ST3      CALL W3SIN3 ( SPEC, CG1, WN2, U10ABS, USTAR, DAIR/DWAT, AS,&
!/ST3                 U10DIR, Z0, CD, TAUWX, TAUWY, VSIN, VDIN, LLWS )
!/STX        CALL W3SINX
!
! 2.b Nonlinear interactions.
!
!/NL1        CALL W3SNL1 ( SPEC, CG1, WNMEAN*DEPTH,          VSNL, VDNL )
!/NL2        CALL W3SNL2 ( SPEC, CG1, DEPTH,                 VSNL, VDNL )
!/NLX        CALL W3SNLX 
!
!/TRX        CALL W3STRX
!
! 2.c Dissipation.
!
!/ST1        CALL W3SDS1 ( SPEC, WN2, EMEAN, FMEAN, WNMEAN,  VSDS, VDDS )
!/ST2        CALL W3SDS2 ( SPEC, CG1, WN1, FPI, USTAR, ALPHA,VSDS, VDDS )
!/ST3        CALL W3SDS3 ( SPEC,  WN1, EMEAN, FMEANS, WNMEAN,              &
!/ST3                                  USTAR, USTDIR, DEPTH, VSDS, VDDS )
!/STX        CALL W3SDSX
!
!/DB1        CALL W3SDB1 ( SPEC, WN2, DEPTH, EMEAN, FMEAN, WNMEAN,        &
!/DB1                                                        VSDB, VDDB )
!/DBX        CALL W3SDBX
!
! 2.d Bottom interactions.
!
!/BT1        CALL W3SBT1 ( SPEC, CG1, WN1, DEPTH,            VSBT, VDBT )
!/BTX        CALL W3SBTX
!
!/BS1        CALL W3SBS1 ( SPEC, CG1, WN1, DEPTH, CX, CY,    VSBS, VDBS )
!/BSX        CALL W3SBSX
!
! 2.e Additional sources.
!
!/XXX        CALL W3SXXX
!
! 2.f Dump training data if necessary
!
!/NNT        WRITE (SCREEN,8888) TIME, DTTOT, FLAGNN, QCERR
!/NNT        WRITE (NDSD2,8888) TIME, DTTOT, FLAGNN, QCERR
!/NNT 8888   FORMAT (1X,I8.8,1X,I6.6,F8.1,L2,F8.2)
!/NNT        WRITE (NDSD,ERR=801,IOSTAT=IERR) IX, IY, TIME, NSTEPS,        &
!/NNT                              DTTOT, FLAGNN, DEPTH, U10ABS, U10DIR
!
!/NNT        IF ( FLAGNN ) THEN
!/NNT            DO IK=1, NK
!/NNT              FACNN  = TPI * SIG(IK) / CG1(IK)
!/NNT              DO ITH=1, NTH
!/NNT                IS     = ITH + (IK-1)*NTH
!/NNT                FOUT(IK,ITH) = SPEC(IS) * FACNN
!/NNT                SOUT(IK,ITH) = VSNL(IS) * FACNN
!/NNT                DOUT(IK,ITH) = VDNL(IS)
!/NNT                END DO
!/NNT              END DO
!/NNT            WRITE (NDSD,ERR=801,IOSTAT=IERR) FOUT
!/NNT            WRITE (NDSD,ERR=801,IOSTAT=IERR) SOUT
!/NNT            WRITE (NDSD,ERR=801,IOSTAT=IERR) DOUT
!/NNT          END IF
!
! 3.  Set frequency cut-off ------------------------------------------ *
!
!/ST2        FHIGH  = XFC * FPI
!/ST2        IF ( FLTEST ) WRITE (NDST,9005) FHIGH*TPIINV
        NKH    = MIN ( NK , INT(FACTI2+FACTI1*LOG(MAX(1.E-7,FHIGH))) )
        NKH1   = MIN ( NK , NKH+1 )
        NSPECH = NKH1*NTH
!/T        WRITE (NDST,9021) NKH, NKH1, NSPECH
!
! 4.  Summation of source terms and diagonal term and time step ------ *
!
        DT     = MIN ( DTG-DTTOT , DTMAX )
        AFILT  = MAX ( DAM(NSPEC) , XFLT*AMAX )
!
        DO IS=1, NSPECH
          VS(IS) = VSLN(IS) + VSIN(IS) + VSNL(IS) + VSDS(IS) + VSBT(IS)
!/DB1          VS(IS) = VS(IS) + VSDB(IS)
!/DBX          VS(IS) = VS(IS) + VSDB(IS)
!/TRX          VS(IS) = VS(IS) + VSTR(IS)
!/BS1          VS(IS) = VS(IS) + VSBS(IS)
!/BSX          VS(IS) = VS(IS) + VSBS(IS)
!/XXX          VS(IS) = VS(IS) + VSXX(IS)
          VD(IS) =            VDIN(IS) + VDNL(IS) + VDDS(IS) + VDBT(IS)
!/DB1          VD(IS) = VD(IS) + VDDB(IS)
!/DBX          VD(IS) = VD(IS) + VDDB(IS)
!/TRX          VD(IS) = VD(IS) + VDTR(IS)
!/BS1          VD(IS) = VD(IS) + VDBS(IS)
!/BSX          VD(IS) = VD(IS) + VDBS(IS)
!/XXX          VD(IS) = VD(IS) + VDXX(IS)
          DAMAX  = MIN ( DAM(IS) , MAX ( XREL*SPEC(IS) , AFILT ) )
          AFAC   = 1. / MAX( 1.E-10 , ABS(VS(IS)/DAMAX) )
          DT     = MIN ( DT , AFAC / ( MAX ( 1.E-10,                  &
                         1. + OFFSET*AFAC*MIN(0.,VD(IS)) ) ) )
          END DO
!
        DT     = MAX ( 0.5 , DT )
!
        DTDYN  = DTDYN + DT
!/T        DTRAW  = DT
        IDT    = 1 + INT ( 0.99*(DTG-DTTOT)/DT )
        DT     = (DTG-DTTOT)/REAL(IDT)
        SHAVE  = DT.LT.DTMIN .AND. DT.LT.DTG-DTTOT
        DT     = MAX ( DT , MIN (DTMIN,DTG-DTTOT) )
        HDT    = OFFSET * DT
        DTTOT  = DTTOT + DT
!
!/T        WRITE (NDST,9040) DTRAW, DT, SHAVE
!
! 5.  Increment spectrum --------------------------------------------- *
!
        IF ( SHAVE ) THEN
!
            DO IS=1, NSPECH
              VS(IS) = VS(IS) * DT / MAX ( 1. , (1.-HDT*VD(IS)))
              VS(IS) = SIGN ( MIN (DAM(IS),ABS(VS(IS))) , VS(IS) )
              SPEC(IS) = MAX ( 0. , SPEC(IS)+VS(IS) )
              END DO
!
          ELSE
!
            DO IS=1, NSPECH
              VS(IS) = VS(IS) * DT / MAX ( 1. , (1.-HDT*VD(IS)))
              SPEC(IS) = MAX ( 0. , SPEC(IS)+VS(IS) )
              END DO
!
          END IF
!
! 6.  Add tail ------------------------------------------------------- *
!   a Mean parameters
!
!
!/ST0        CALL W3SPR0 (SPEC, CG1, WN1, EMEAN, FMEAN, WNMEAN, AMAX)
!/ST1        CALL W3SPR1 (SPEC, CG1, WN1, EMEAN, FMEAN, WNMEAN, AMAX)
!/ST2        CALL W3SPR2 (SPEC, CG1, WN1, DEPTH, FPI, U10ABS, USTAR,   &
!/ST2                     EMEAN, FMEAN, WNMEAN, AMAX, ALPHA, FP )
!/ST3        CALL W3SPR3 (SPEC, CG1, WN1, EMEAN, FMEAN, FMEANS,        &
!/ST3                     WNMEAN, AMAX, U10ABS, U10DIR, USTAR, USTDIR, &
!/ST3                     TAUWX, TAUWY, CD, Z0, LLWS, FMEANWS)
!
!/FLX2        CALL W3FLX2 ( ZWND, DEPTH, FP, U10ABS, U10DIR,           &
!/FLX2                                          USTAR, USTDIR, Z0, CD )
!/FLX3        CALL W3FLX3 ( ZWND, DEPTH, FP, U10ABS, U10DIR,           &
!/FLX3                                          USTAR, USTDIR, Z0, CD )
!
!/ST1        FH1    = FXFM * FMEAN
!/ST1        FHIGH  = MIN ( SIG(NK) , MAX ( FH1 , FH2 ) )
!/ST1        NKH    = MAX ( 2 , MIN ( NKH1 ,                           &
!/ST1                 INT ( FACTI2 + FACTI1*LOG(MAX(1.E-7,FHIGH)) ) ) )
!
!/ST1        IF ( FLTEST ) WRITE (NDST,9060)                           &
!/ST1                      FH1*TPIINV, FH2*TPIINV, FHIGH*TPIINV, NKH
!
!/ST2        FHTRAN = XFT*FPI
!/ST2        FHIGH  = XFC*FPI
!/ST2        DFH    = FHIGH - FHTRAN
!/ST2        NKH    = MAX ( 1 ,                                        &
!/ST2            INT ( FACTI2 + FACTI1*LOG(MAX(1.E-7,FHTRAN)) ) )
!
!/ST2        IF ( FLTEST ) WRITE (NDST,9061) FHTRAN, FHIGH, NKH
!
!/ST3        FH1    = FXFM * FMEAN
!/ST3        FH2    = FXPM / USTAR
!/ST3        FHIGH  = MIN ( SIG(NK) , MAX ( FH1 , FH2 ) )
!/ST3        NKH    = MAX ( 2 , MIN ( NKH1 ,                           &
!/ST3                 INT ( FACTI2 + FACTI1*LOG(MAX(1.E-7,FHIGH)) ) ) )
!
!/ST3        IF ( FLTEST ) WRITE (NDST,9062)                           &
!/ST3                      FH1*TPIINV, FH2*TPIINV, FHIGH*TPIINV, NKH
!
! 6.b Limiter for shallow water or Miche style criterion
!     Last time step only !
!
!/MLIM        IF ( DTTOT .GE. 0.9999*DTG ) THEN
!/MLIM            HM     = FHMAX / WNMEAN * TANH(WNMEAN*MAX(0.,DEPTH))
!/MLIM            EM     = HM * HM / 16.
!/MLIM            IF ( EMEAN.GT.EM .AND. EMEAN.GT.1.E-30 ) THEN
!/MLIM                SPEC   = SPEC / EMEAN * EM
!/MLIM                EMEAN  = EM
!/MLIM              END IF
!/MLIM          END IF
!
! 6.c Seeding of spectrum
!     alpha = 0.005 , 0.5 in eq., 0.25 for directional distribution
!
!/SEED        DO IK=MIN(NK,NKH), NK
!/SEED          UC     = FACSD * GRAV / SIG(IK)
!/SEED          SLEV   = MIN ( 1. , MAX ( 0. , U10ABS/UC-1. ) ) *      &
!/SEED                       6.25E-4 / WN1(IK)**3 / SIG(IK)
!/SEED          DO ITH=1, NTH
!/SEED            SPEC(ITH+(IK-1)*NTH) = MAX ( SPEC(ITH+(IK-1)*NTH) ,  &
!/SEED                  SLEV * MAX ( 0. , COS(U10DIR-TH(ITH)) )**2 )
!/SEED            END DO
!/SEED          END DO
!
! 6.d Add tail
!
        DO IK=NKH+1, NK
!/ST2          FACDIA = MAX ( 0. , MIN ( 1., (SIG(IK)-FHTRAN)/DFH) )
!/ST2          FACPAR = MAX ( 0. , 1.-FACDIA )
          DO ITH=1, NTH
            SPEC(ITH+(IK-1)*NTH) = SPEC(ITH+(IK-2)*NTH) * FACHFA          &
!/ST2                * FACDIA + FACPAR * SPEC(ITH+(IK-1)*NTH)             &
                       + 0.
            END DO
          END DO
!
! 6.e  Update wave-supported stress----------------------------------- *
!
!/ST3        CALL W3SIN3 ( SPEC, CG1, WN2, U10ABS, USTAR, DAIR/DWAT, AS,&
!/ST3                 U10DIR, Z0, CD, TAUWX, TAUWY, VSIN, VDIN, LLWS )
!
! 7.  Check if integration complete ---------------------------------- *
!
        IF ( DTTOT .GE. 0.9999*DTG ) EXIT
        END DO
!
! ... End point dynamic integration - - - - - - - - - - - - - - - - - -
!
! 8.  Save integration data ------------------------------------------ *
!
      DTDYN  = DTDYN / REAL(MAX(1,NSTEPS))
      FCUT   = FHIGH * TPIINV
!
      GOTO 888
!
! Error escape locations
!
!/NNT  800 CONTINUE
!/NNT      WRITE (NDSE,8000) FNAME, IERR
!/NNT      CALL EXTCDE (1)
!
!/NNT  801 CONTINUE
!/NNT      WRITE (NDSE,8001) IERR
!/NNT      CALL EXTCDE (2)
!
  888 CONTINUE
      RETURN
!
! Formats
!
!/NNT 8000 FORMAT (/' *** ERROR W3SRCE : ERROR IN OPENING FILE ',A,' ***'/ &
!/NNT               '                    IOSTAT = ',I10/)
!/NNT 8001 FORMAT (/' *** ERROR W3SRCE : ERROR IN WRITING TO FILE ***'/    &
!/NNT               '                    IOSTAT = ',I10/)
!
!/T 9000 FORMAT (' TEST W3SRCE : COUNTERS   : NO LONGER AVAILABLE')
!/T 9001 FORMAT (' TEST W3SRCE : DEPTH      :',F8.1/                     &
!/T              '               WIND SPEED :',F8.1/                     &
!/T              '               WIND DIR   :',F8.1)
!/ST1 9004 FORMAT (' TEST W3SRCE : FHIGH (3X) : ',3F8.4/                   &
!/ST1              ' ------------- NEW DYNAMIC INTEGRATION LOOP',          &
!/ST1              ' ------------- ')
!/ST2 9005 FORMAT (' TEST W3SRCE : FHIGH      : ',F8.4/                    &
!/ST2              ' ------------- NEW DYNAMIC INTEGRATION LOOP',          &
!/ST2              ' ------------- ')
!/ST3 9006 FORMAT (' TEST W3SRCE : FHIGH (3X) : ',3F8.4/                   &
!/ST3              ' ------------- NEW DYNAMIC INTEGRATION LOOP',          &
!/ST3              ' ------------- ')
!
!/T 9020 FORMAT (' TEST W3SRCE : NSTEP : ',I4,'    DTTOT :',F6.1)
!/T 9021 FORMAT (' TEST W3SRCE : NKH (3X)   : ',2I3,I6)
!
!/T 9040 FORMAT (' TEST W3SRCE : DTRAW, DT, SHAVE :',2F6.1,2X,L1)
!
!/ST1 9060 FORMAT (' TEST W3SRCE : FHIGH (3X) : ',3F8.4/                   &
!/ST1              '               NKH        : ',I3)
!/ST2 9061 FORMAT (' TEST W3SRCE : FHIGH (2X) : ',2F8.4/                   &
!/ST2              '               NKH        : ',I3)
!/ST3 9062 FORMAT (' TEST W3SRCE : FHIGH (3X) : ',3F8.4/                   &
!/ST3              '               NKH        : ',I3)
!/
!/ End of W3SRCE ----------------------------------------------------- /
!/
      END SUBROUTINE W3SRCE
!/
!/ End of module W3SRCEMD -------------------------------------------- /
!/
      END MODULE W3SRCEMD
